allprojects {
    group 'com.k2data.platform'
    version '1.0'

    apply plugin: 'java'

    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    ext {
        log4jVersion = 2.7
        persistenceApiVersion = '1.0.2'
        servletApiVersion = '3.1.0'
        mysqlVersion = '5.1.30'
        mybatisVersion = '3.4.1'
        cglibVersion = '3.2.4'
    }

    def loggerLibraries = [
            "org.apache.logging.log4j:log4j-core:${log4jVersion}",
            "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    ]

    dependencies {
        compile loggerLibraries
        compile "javax.persistence:persistence-api:${persistenceApiVersion}"
        compile "javax.servlet:javax.servlet-api:${servletApiVersion}"
        compile "mysql:mysql-connector-java:${mysqlVersion}"
        compile "org.mybatis:mybatis:${mybatisVersion}"
        compile "cglib:cglib:${cglibVersion}"
        compile group: 'com.alibaba', name: 'fastjson', version:'1.2.20'
        compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.5'
        compile group: 'org.apache.commons', name: 'commons-io', version:'1.3.2'
        compile group: 'commons-beanutils', name: 'commons-beanutils', version:'1.9.1'
        compile group: 'com.google.guava', name: 'guava', version:'20.0'
        testCompile 'junit:junit:4.12'
    }
}

project("k2azkaban-job-common") {
    task copyJars(type: Copy) {
        from configurations.runtime
        into 'lib'
    }
}

task packageZip(type: Zip, dependsOn: build) {
    from('k2azkaban-job-common/build/libs') {
        into('lib')
    }
    from('k2azkaban-job-common/lib') {
        into('lib')
    }
    from('k2azkaban-job-jobs/build/resources/main/') {
        into('/')
    }
    from('k2azkaban-job-jobs/build/libs') {
        into('/')
    }
}
packageZip.dependsOn(project("k2azkaban-job-common").copyJars)
packageZip.dependsOn(subprojects.build)




